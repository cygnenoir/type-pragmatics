module names

imports
  include/STLC
  
namespaces Module Sort Symbol Axiom Rule

binding rules

	mod@Module(m, _, _):
		defines unique Module m
			of module-ref mod
		implicitly defines Sort "Bool"
		scopes Sort, Symbol, Axiom, Rule
		
	Import(m,_):
		imports Sort, Symbol, Axiom from Module m
		// refers to Module m
		// where m has module-ref mod

	Local(_):
		scopes Sort, Symbol, Axiom, Rule

	Goals(_):
		scopes Rule

	RuleName(_, r):
		defines Rule r

	SortDef(s):
		defines Sort s
	DataType(s, _):
		defines Sort s
	
	SortRef(s):
		refers to Sort s
	
	ConstructorDecl(c, _, _):
		defines Symbol c
	DataTypeConstructor(c, _):
		defines Symbol c

	FunctionDef(FunctionSig(f, _, _),_):
		defines Symbol f
	FunctionEq(f, pat, body):
		scopes Symbol
		
	FunctionPatVar(v):
		refers to Symbol v
		defines non-unique Symbol v
	FunctionPatApp(f, args):
		refers to Symbol f

	FunctionExpVar(v):
		refers to Symbol v
	FunctionExpApp(f, args):
		refers to Symbol f
	FunctionExpLet(v, e1, e2):
		defines Symbol v in e2

properties

  module-ref of Module : ModuleRef
  