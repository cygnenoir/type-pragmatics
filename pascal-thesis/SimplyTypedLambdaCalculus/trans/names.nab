module names

imports
  include/STLC
  
namespaces Module Sort Symbol Axiom Goal

binding rules

	mod@Module(m, _, _):
		defines unique Module m
			of module-ref mod
		implicitly defines Sort "Bool"
		scopes Sort, Symbol, Axiom, Goal
		
	Import(m):
		imports Sort, Symbol, Axiom from Module m
		// refers to Module m
		// where m has module-ref mod

	Local(_):
		scopes Sort, Symbol, Axiom, Goal

	SortDef(s):
		defines Sort s
	SortRef(s):
		refers to Sort s
	
	ConstructorDecl(c, _, _):
		defines Symbol c

	FunctionDef(FunctionSig(f, _, _),_):
		defines Symbol f
	FunctionEq(f, pat, body):
		scopes Symbol
		
	FunctionPatVar(v):
		refers to Symbol v
		defines Symbol v
	FunctionPatApp(f, args):
		refers to Symbol f

	FunctionExpVar(v):
		refers to Symbol v
	FunctionExpApp(f, args):
		refers to Symbol f
	FunctionExpLet(v, e1, e2):
		defines Symbol v in e2

properties

  module-ref of Module : ModuleRef