module stlca.Subst

import stlca.Syntax
import stlca.Context
import stlca.Gensym

function
subst : Var Exp Exp -> Exp
subst(x, e, True) = True
subst(x, e, False) = False
subst(x, e, Zero) = Zero
subst(x, e, succ(e1)) = succ(subst(x, e, e1))
subst(x, e, pred(e1)) = pred(subst(x, e, e1))
subst(x, e, isZero(e1)) = isZero(subst(x, e, e1))
subst(x, e, var(y)) =
	if x == y
	then e
	else var(y)
subst(x, e, app(e1, e2)) = app(subst(x, e, e1), subst(x, e, e2))
subst(x, e, abs(y, T, e1)) =
	if x == y
	then abs(y, T, e1)
	else if isFreeVar(y, e)
	     then let fresh = gensym(app(app(e, e1), var(x)))
	          in subst(x, e, abs(fresh, T, subst(y, var(fresh), e1))) 
	     else abs(y, T, subst(x, e, e1))
