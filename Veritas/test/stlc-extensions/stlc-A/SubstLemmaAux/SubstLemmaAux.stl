module stlca.SubstLemmaAux.SubstLemmaAux

import stlca.Syntax
import stlca.Context
import stlca.Subst
import stlca.AlphaEquivalence
import stlca.Types


//Auxiliary lemmas for on contexts

//Proof by structural induction 

goal
~e == True
!isFreeVar(~x, ~e)
~C |- ~e : ~T
=========================== T-Weak-FreeVar-true
bind(~x, ~S, ~C) |- ~e : ~T

goal
~e == False
!isFreeVar(~x, ~e)
~C |- ~e : ~T
=========================== T-Weak-FreeVar-false
bind(~x, ~S, ~C) |- ~e : ~T

goal
~e == Zero
!isFreeVar(~x, ~e)
~C |- ~e : ~T
=========================== T-Weak-FreeVar-zero
bind(~x, ~S, ~C) |- ~e : ~T

local {
	const esucc : Exp
	
	axiom
	!isFreeVar(~x, esucc)
	~C |- esucc : ~T
	============================== T-Weak-FreeVar-succ-IH
	bind(~x, ~S, ~C) |- esucc : ~T
	
	goal
	~e == succ(esucc)
	!isFreeVar(~x, ~e)
	~C |- ~e : ~T
	============================== T-Weak-FreeVar-succ
	bind(~x, ~S, ~C) |- ~e : ~T
}

local {
	const epred : Exp
	
	axiom
	!isFreeVar(~x, epred)
	~C |- epred : ~T
	============================== T-Weak-FreeVar-pred-IH
	bind(~x, ~S, ~C) |- epred : ~T
	
	goal
	~e == pred(epred)
	!isFreeVar(~x, ~e)
	~C |- ~e : ~T
	============================== T-Weak-FreeVar-pred
	bind(~x, ~S, ~C) |- ~e : ~T
}

local {
	const eisZero : Exp
	
	axiom
	!isFreeVar(~x, eisZero)
	~C |- eisZero : ~T
	============================== T-Weak-FreeVar-isZero-IH
	bind(~x, ~S, ~C) |- eisZero : ~T
	
	goal
	~e == isZero(eisZero)
	!isFreeVar(~x, ~e)
	~C |- ~e : ~T
	============================== T-Weak-FreeVar-isZero
	bind(~x, ~S, ~C) |- ~e : ~T
}

local {
	const eabs : Exp
	
	// proof in T-Weak-FreeVar-abs1.stl
	axiom
	~x != ~y
	~e == abs(~y, ~S1, ~e1)
	!isFreeVar(~x, ~e)
	~C |- ~e : ~T
	============================= T-Weak-FreeVar-abs-1
	bind(~x, ~S, ~C) |- ~e : ~T
	
	
	//necessary axioms on alpha equivalence
	axioms
	============================ alpha-equiv-refl
	alphaEquivalent(~e, ~e)


	alphaEquivalent(~e1, ~e2)
	============================ alpha-equiv-sym
	alphaEquivalent(~e2, ~e1)

 
	alphaEquivalent(~e1, ~e2)
	alphaEquivalent(~e2, ~e3)
	============================ alpha-equiv-trans
	alphaEquivalent(~e1, ~e3)

	!isFreeVar(~y, ~e)
	====================================================================== alpha-equiv-subst-abs
	alphaEquivalent(abs(~x, ~S, ~e), abs(~y, ~S, subst(~x, var(~y), ~e)))


	~C |- ~e : ~T
	alphaEquivalent(~e, ~e1)
	========================= alpha-equiv-typing
	~C |- ~e1 : ~T
	
	!isFreeVar(~x, ~e)
	alphaEquivalent(~e, ~e1)
	========================= alpha-equiv-FreeVar
	!isFreeVar(~x, ~e1)

	//NOT needed!
	axiom
	!isFreeVar(~x, eabs)
	~C |- eabs : ~T
	============================== T-Weak-FreeVar-abs-IH
	bind(~x, ~S, ~C) |- eabs : ~T

	goal
	~x == ~y
	~e == abs(~y, ~S1, eabs)
	!isFreeVar(~x, ~e)
	~C |- ~e : ~T
	============================ T-Weak-FreeVar-abs-2
	bind(~x, ~S, ~C) |- ~e : ~T
}

local {
	consts
		e1app : Exp
		e2app : Exp
		
	axioms
	!isFreeVar(~x, e1app)
	~C |- e1app : ~T
	=============================== T-Weak-FreeVar-app-IH1
	bind(~x, ~S, ~C) |- e1app : ~T
	
	!isFreeVar(~x, e2app)
	~C |- e2app : ~T
	=============================== T-Weak-FreeVar-app-IH2
	bind(~x, ~S, ~C) |- e2app : ~T
	
	
	goal
	~e == app(e1app, e2app)
	!isFreeVar(~x, ~e)
	~C |- ~e : ~T
	=========================== T-Weak-FreeVar-app
	bind(~x, ~S, ~C) |- ~e : ~T
}

