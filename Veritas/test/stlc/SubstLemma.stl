module stlc.SubstLemma

import stlc.Syntax
import stlc.Context
import stlc.Types
import stlc.Gensym
import stlc.Subst
import stlc.SubstLemmaAux

strategy subst-abs-3-alpha-equiv {
	
	import stlc.AlphaEquivalence
	
	// generalized axiom for abs-2 case
	axiom
	~x != ~y
	!isFreeVar(~y, ~e)
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- abs(~y, ~S, ~e1) : ~T2
	================================================= T-subst-abs-2-gen
	~C |- subst(~x, ~e, abs(~y, ~S, ~e1)) : ~T2
	
	//auxiliary lemma which is needed in this case, but can also be proven
	lemma
	~fresh == gensym(app(app(~e, ~e1), var(~x)))
	============================================= fresh-unequal-var-3
	~x != ~fresh

	//another auxiliary lemma which is needed in this case, but can also be proven
	lemma
	~fresh == gensym(app(app(~e, ~e1), var(~x)))
	============================================= fresh-free-2
	!isFreeVar(~fresh, ~e1)
	
}


goal
~C |- ~e : ~T
bind(~x, ~T, ~C) |- var(~y) : ~T2
============================== T-subst-var
~C |- subst(~x, ~e, var(~y)) : ~T2


local {
	consts
		e1 : Exp
		e2 : Exp
	
	axioms
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- e1 : ~T2
	============================== T-subst-IH-app1
	~C |- subst(~x, ~e, e1) : ~T2
	
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- e2 : ~T2
	============================== T-subst-IH-app2
	~C |- subst(~x, ~e, e2) : ~T2
	
	goal
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- app(e1, e2) : ~T2
	====================================== T-subst-app
	~C |- subst(~x, ~e, app(e1, e2)) : ~T2
}

local {
	const eabs : Exp
	
	axiom
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- eabs : ~T2
	============================== T-subst-IH-abs
	~C |- subst(~x, ~e, eabs) : ~T2
	
	// prove abs-case by case split!
	lemma
	~x == ~y
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- abs(~y, ~S, ~e1) : ~T2
	=========================================== T-subst-abs-1
	~C |- subst(~x, ~e, abs(~y, ~S, ~e1)) : ~T2

	lemma
	~x != ~y
	!isFreeVar(~y, ~e)
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- abs(~y, ~S, eabs) : ~T2
	================================================= T-subst-abs-2
	~C |- subst(~x, ~e, abs(~y, ~S, eabs)) : ~T2
	
	lemma verify-with subst-abs-3-alpha-equiv
	~x != ~y
	isFreeVar(~y, ~e)
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- abs(~y, ~S, eabs) : ~T2
	================================================= T-subst-abs-3
	~C |- subst(~x, ~e, abs(~y, ~S, eabs)) : ~T2
	
	goal
	~C |- ~e : ~T
	bind(~x, ~T, ~C) |- abs(~y, ~S, eabs) : ~T2
	================================================= T-subst-abs
	~C |- subst(~x, ~e, abs(~y, ~S, eabs)) : ~T2
}

// substitution lemma becomes an axiom after the proof
axiom
~C |- ~e : ~T
bind(~x, ~T, ~C) |- ~e2 : ~T2
============================== T-subst
~C |- subst(~x, ~e, ~e2) : ~T2


