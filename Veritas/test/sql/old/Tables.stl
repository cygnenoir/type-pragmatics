module sql.Tables

// explanation:
// FType: type of a single field (underspecified)
// Name: name of attribute (underspecified)
// AttrL: list of attribute names
// CType: type of a single column (pair Name, FType)
// TType: type of a table (table schema) - list of CType


sorts
	FType Name AttrL CType TType RawType
	
constructors
	ctype : Name FType -> CType
	aempty : AttrL
	acons : Name AttrL -> AttrL
	ttempty : TType
	ttcons : CType TType -> TType
	rtempty : RawType
	rtcons : FType RawType -> RawType

//explanation:
// Val: Value for a field (underspecified)
// Row: table row, list of field values (with at least one cell/field per construction!)
// RawTable: table matrix (list of rows), without "header" (attribute list)
// Table: full table with "header" (attribute list) 
	
sorts
	Val Row RawTable Table 

constructors
	rempty : Row
	rcons : Val Row -> Row
	tempty : RawTable
	tcons : Row RawTable -> RawTable
	table : AttrL RawTable -> Table

function
getRaw : Table -> RawTable
getRaw(table(al, rt)) = rt

function 
getAttrL : Table -> AttrL
getAttrL(table(al, rt)) = al

// function that assigns a field type to every field value  (underspecified)	
function
fieldType : Val -> FType

// function that compares whether first field value is smaller than second field value 
// (underspecified)
function
le : Val Val -> Bool

// function that compares whether first field value is greater than second field value 
// (underspecified)
function
ge : Val Val -> Bool

// check whether a table corresponds to a given type (functional notation)
// does not yet check for whether the table type contains only unique attribute names!!
// (but semantics should be possible to define in a sensible way without that requirement...)
	
function 
matchingAttrL : TType AttrL -> Bool
matchingAttrL(ttempty, aempty) = true
matchingAttrL(ttcons(ctype(a1, f), tt), acons(a2, al)) = 
	(a1 == a2) && matchingAttrL(tt, al)
matchingAttrL(tt, al) = false	
	
function
welltypedtable : TType Table -> Bool
welltypedtable(tt, table(al, t)) = matchingAttrL(tt, al) && welltypedRawtable(rawType(tt), t)

function
rawType : TType -> RawType
rawType(ttempty) = rtempty
rawType(ttcons(ctype(a, ft), tt)) = rtcons(ft, rawType(tt))

function
welltypedRawtable : RawType RawTable -> Bool
welltypedRawtable(rt, tempty) = true
welltypedRawtable(rt, tcons(r, t)) = welltypedRow(rt, r) && welltypedRawtable(rt, t)

function
welltypedRow : RawType Row -> Bool
welltypedRow(rtempty, rempty) = true
welltypedRow(rtcons(ft, rt), rcons(v, r)) = (fieldType(v) == ft) && welltypedRow(rt, r)
welltypedRow(rt, r) = false
	
