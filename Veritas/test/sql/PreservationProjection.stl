module sql.PreservationProjection

import sql.BasicFunctions
import sql.Syntax
import sql.Semantics
import sql.TContext
import sql.TypeSystem
import sql.TypeSystemInv
import sql.TStore
import sql.Tables
import sql.TableAux
import sql.SoundnessAuxDefs

// proof of ProjectEmptyCol-RTEmpty
strategy induct-projectemptycol-rtempty {
	// by structural induction ~rt
	
	goal
	~rt = tempty
	================================================ ProjectEmptyCol-RTEmpty-1
	welltypedRawtable(rtempty, projectEmptyCol(~rt))
	
	local {
		consts
			r : Row
			rest : RawTable
		
		axiom
		~rt = rest
		================================================ ProjectEmptyCol-RTEmpty-2-IH
		welltypedRawtable(rtempty, projectEmptyCol(~rt))
		
		goal
		~rt = tcons(r, rest)
		================================================ ProjectEmptyCol-RTEmpty-2
		welltypedRawtable(rtempty, projectEmptyCol(~rt))
	}
	
	hide-all
	axiom
	true
	================================================ ProjectEmptyCol-RTEmpty-ind
	welltypedRawtable(rtempty, projectEmptyCol(~rt))
	
} // qed ProjectEmptyCol-RTEmpty

lemma verify-with induct-projectemptycol-rtempty
true
================================================ ProjectEmptyCol-RTEmpty
welltypedRawtable(rtempty, projectEmptyCol(~rt))

// proof of DropFirstCol-Preservation
strategy induction-dropfirstcol-pres  {
	// by structural induction on ~rt
	
	goal
	~rt = tempty
	welltypedRawtable(rtcons(~ft, ~RT), ~rt)
	===================================================== DropFirstCol-Preservation-1
	welltypedRawtable(~RT, dropFirstColRaw(~rt))
	
	local {
		consts
			r : Row
			rt : RawTable
		
		axiom
		welltypedRawtable(rtcons(~ft, ~RT), rt)
		===================================================== DropFirstCol-Preservation-2-IH
		welltypedRawtable(~RT, dropFirstColRaw(rt))
		
		goal
		//~FOO = tcons(r, rt) // XXX including this equation -> no proof found
		welltypedRawtable(rtcons(~ft, ~RT), tcons(r, rt))
		===================================================== DropFirstCol-Preservation-2
		welltypedRawtable(~RT, dropFirstColRaw(tcons(r, rt)))
	}
	
	hide-all
	axiom
	welltypedRawtable(rtcons(~ft, ~RT), ~rt)
	===================================================== DropFirstCol-Preservation-ind
	welltypedRawtable(~RT, dropFirstColRaw(~rt))
	
} // qed DropFirstCol-Preservation

lemma verify-with induction-dropfirstcol-pres 
welltypedRawtable(rtcons(~ft, ~RT), ~rt)
===================================================== DropFirstCol-Preservation
welltypedRawtable(~RT, dropFirstColRaw(~rt))


lemma
isSome(attachColToFrontRaw(tcons(~r, ~col), ~rt))
================================================================= AttachColToFrontRaw-Aux 
exists ~f, ~rthead, ~rtrest
	~r = rcons(~f, rempty)
	~rt = tcons(~rthead, ~rtrest)
	isSome(attachColToFrontRaw(~col, ~rtrest))
	attachColToFrontRaw(tcons(~r, ~col), ~rt) = some(tcons(rcons(~f, ~rthead), getSome(attachColToFrontRaw(~col, ~rtrest))))

lemma
~r = rcons(~f, rempty)
~rt = tcons(~rthead, ~rtrest)
welltypedRawtable(~RT, ~rt)
welltypedRawtable(rtcons(~colType, rtempty), tcons(~r, ~col))
welltypedRawtable(rtcons(~colType, ~RT), getSome(attachColToFrontRaw(~col, ~rtrest)))
================================================================================================================ AttachCol-WelltypedRawtable-Aux
welltypedRawtable(rtcons(~colType, ~RT), tcons(rcons(~f, ~rthead), getSome(attachColToFrontRaw(~col, ~rtrest))))


// proof of AttachCol-Preserves-Typing
strategy induction-attachcol-preserves-typing {
	// by structural induction on ~col
	
	goal
	~col = tempty
	welltypedRawtable(~RT, ~rt)
	welltypedRawtable(rtcons(~colType, rtempty), ~col)
	isSome(attachColToFrontRaw(~col, ~rt))
	======================================================================== AttachCol-Preserves-Typing-1
	welltypedRawtable(rtcons(~colType, ~RT), getSome(attachColToFrontRaw(~col, ~rt)))
	
	local {
		consts
			r : Row
			col : RawTable
		
		axiom
		welltypedRawtable(~RT, ~rt)
		welltypedRawtable(rtcons(~colType, rtempty), col)
		isSome(attachColToFrontRaw(col, ~rt))
		======================================================================== AttachCol-Preserves-Typing-2-IH
		welltypedRawtable(rtcons(~colType, ~RT), getSome(attachColToFrontRaw(col, ~rt)))
		
		goal
		welltypedRawtable(~RT, ~rt)
		welltypedRawtable(rtcons(~colType, rtempty), tcons(r, col))
		isSome(attachColToFrontRaw(tcons(r, col), ~rt))
		======================================================================== AttachCol-Preserves-Typing-2
		welltypedRawtable(rtcons(~colType, ~RT), getSome(attachColToFrontRaw(tcons(r, col), ~rt)))
	}
	
	hide-all
	axiom
	welltypedRawtable(~RT, ~rt)
	welltypedRawtable(rtcons(~colType, rtempty), ~col)
	isSome(attachColToFrontRaw(~col, ~rt))
	======================================================================== AttachCol-Preserves-Typing-ind
	welltypedRawtable(rtcons(~colType, ~RT), getSome(attachColToFrontRaw(~col, ~rt)))
} // qed AttachCol-Preserves-Typing

lemma verify-with induction-attachcol-preserves-typing
welltypedRawtable(~RT, ~rt)
welltypedRawtable(rtcons(~colType, rtempty), ~col)
isSome(attachColToFrontRaw(~col, ~rt))
======================================================================== AttachCol-Preserves-Typing
welltypedRawtable(rtcons(~colType, ~RT), getSome(attachColToFrontRaw(~col, ~rt)))


// proof of ProjectFirstRaw-Preservation (auxiliary lemma needed in proof of FindCol Preservation)
strategy induction-projectfirstraw-pres {
	// by structural induction on ~rt
	
	goal
	~rt = tempty
	welltypedRawtable(rtcons(~colType, ~RT), ~rt)
	=================================================================== ProjectFirstRaw-Preservation-1
	welltypedRawtable(rtcons(~colType, rtempty), projectFirstRaw(~rt))
	
	local {
		consts
			r : Row
			rt : RawTable
		
		hide-all
		
		axiom
		welltypedRawtable(rtcons(~colType, ~RT), rt)
		================================================================= ProjectFirstRaw-Preservation-2-IH
		welltypedRawtable(rtcons(~colType, rtempty), projectFirstRaw(rt))
		
		include {EQ-ctype, EQ-aempty, EQ-acons, EQ-ttempty, EQ-ttcons, EQ-rtempty, 
		EQ-rtcons, DIFF-aempty-acons, DIFF-ttempty-ttcons, DIFF-rtempty-rtcons,
		EQ-rempty, EQ-rcons, EQ-tempty, EQ-tcons, EQ-table, DIFF-rempty-rcons,
		DIFF-tempty-tcons, welltypedRawtable0, welltypedRawtable1, welltypedRow0,
		welltypedRow1, welltypedRow2, projectFirstRaw0, projectFirstRaw1, projectFirstRaw2,
		projectFirstRaw-INV}
		
		goal
		welltypedRawtable(rtcons(~colType, ~RT), tcons(r, rt))
		============================================================================ ProjectFirstRaw-Preservation-2
		welltypedRawtable(rtcons(~colType, rtempty), projectFirstRaw(tcons(r, rt)))
	}
	
	hide-all
	axiom
	welltypedRawtable(rtcons(~colType, ~RT), ~rt)
	============================================================================== ProjectFirstRaw-Preservation-ind
	welltypedRawtable(rtcons(~colType, rtempty), projectFirstRaw(~rt))
	
} // qed ProjectFirstRaw-Preservation

// proof of FindCol-Preservation
strategy induct-findCol-Preservation {
	// by induction on recursive structure of findCol
		
	goal
	~al = aempty
	~t = table(~al, ~rt)
	welltypedtable(~TT, ~t)
	findCol(~a, ~t) = some(~col)
	findColType(~a, ~TT) = some(~colType)
	============================================================== FindCol-Preservation-1
	welltypedRawtable(rtcons(~colType, rtempty), ~col)

	local {
		consts
			a : AName
			a2 : AName
			al : AList
			rt : RawTable
		
		axiom
		findCol(a, table(al, dropFirstColRaw(rt))) = some(~col)
		welltypedtable(~TT, table(al, dropFirstColRaw(rt)))
		findColType(a, ~TT) = some(~colType)
		============================================================== FindCol-Preservation-2-IH
		welltypedRawtable(rtcons(~colType, rtempty), ~col)
		
		lemma verify-with induction-projectfirstraw-pres
		welltypedRawtable(rtcons(~colType, ~RT), ~rt)
		============================================================================== ProjectFirstRaw-Preservation
		welltypedRawtable(rtcons(~colType, rtempty), projectFirstRaw(~rt))
		
		
		goal
		welltypedtable(~TT, table(acons(a2, al), rt))
		findCol(a, table(acons(a2, al), rt)) = some(~col)
		findColType(a, ~TT) = some(~colType)
		============================================================== FindCol-Preservation-2
		welltypedRawtable(rtcons(~colType, rtempty), ~col)
	}
	
	hide-all
	axiom
	// ~t = table(~al, ~rt)
	welltypedtable(~TT, ~t)
	isSome(findCol(~a, ~t))
	isSome(findColType(~a, ~TT))
	================================================== FindCol-Preservation-ind
	welltypedRawtable(rtcons(getSome(findColType(~a, ~TT)), rtempty), getSome(findCol(~a, ~t)))
	
} //qed FindCol-Preservation

lemma verify-with induct-findCol-Preservation
// ~t = table(~al, ~rt)
welltypedtable(~TT, ~t)
isSome(findCol(~a, ~t))
isSome(findColType(~a, ~TT))
================================================== FindCol-Preservation
welltypedRawtable(rtcons(getSome(findColType(~a, ~TT)), rtempty), getSome(findCol(~a, ~t)))

// proof of project-preservation
strategy struc-induction-project-preservation {
	// proof of Project-Preservation by structural induction on ~al

	goal
	~al = aempty
	welltypedtable(~TT, ~t)
	project(~al, ~t) == some(~pt)
	projectType(~al, ~TT) == some(~PT)
	===================================== Project-Preservation-aempty
	~TTC |- Tvalue(table(~al, ~pt)) : ~PT
	
	local {
		consts
			a : AName
			alrest : AList
		
		axiom
		~al = alrest
		welltypedtable(~TT, ~t)
		isSome(project(~al, ~t))
		isSome(projectType(~al, ~TT))
		===================================== Project-Preservation-acons-IH
		~TTC |- Tvalue(table(~al, getSome(project(~al, ~t)))) : getSome(projectType(~al, ~TT))
		
		goal
		~al = acons(a, alrest)
		welltypedtable(~TT, ~t)
		isSome(project(~al, ~t))
		isSome(projectType(~al, ~TT))
		======================================================================================= Project-Preservation-acons
		~TTC |- Tvalue(table(~al, getSome(project(~al, ~t)))) : getSome(projectType(~al, ~TT))
	}
	
	//if induction successful, we proved Project-Preservation and can abbreviate (for now:)
	hide-all
	axiom
	welltypedtable(~TT, ~t)
	project(~al, ~t) = some(~pt)
	projectType(~al, ~TT) == some(~PT)
	===================================== Project-Preservation-ind
	~TTC |- Tvalue(table(~al, ~pt)) : ~PT	
	
} // qed Project-Preservation

lemma verify-with struc-induction-project-preservation
welltypedtable(~TT, ~t)
project(~al, ~t) = some(~pt)
projectType(~al, ~TT) == some(~PT)
===================================== Project-Preservation
~TTC |- Tvalue(table(~al, ~pt)) : ~PT
